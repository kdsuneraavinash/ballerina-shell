plugins {
    id 'java'
}

group 'io.ballerina'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    flatDir {
        dirs "${rootProject.projectDir}/libs"
    }
}

dependencies {
    implementation project(":shell")
    implementation(group: 'commons-cli', name: 'commons-cli', version: '1.3.1') {
        because("To handle command line arguments")
    }
    implementation(group: 'org.jline', name: 'jline', version: '3.17.1') {
        because("To implement the REPL CLI functionalities")
    }
    testImplementation('org.testng:testng:6.14.3') {
        because("To test the core functionality")
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

def mainCliClass = 'io.ballerina.shell.cli.ReplShellApplication'
task run(type: JavaExec) {
    main = mainCliClass
    standardInput = System.in
    classpath = sourceSets.main.runtimeClasspath
}

task fatJar(type: Jar) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    manifest { attributes 'Main-Class': mainCliClass }
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

ext.moduleName = 'io.ballerina.shell.cli'
